#@TYPE: Machine
#@NAME: Dynamic Devices i.MX 8M Mini Jaguar Sentai board with LPDDR4
#@SOC: i.MX8MM
#@DESCRIPTION: Machine configuration for Dynamic Devices i.MX 8M Mini Jaguar Sentai board with LPDDR4
#@MAINTAINER: Alex J Lennon  <ajlennon@dynamicdevices.co.uk>

MACHINEOVERRIDES =. "imx8mm-lpddr4-evk:"

require conf/machine/imx8mm-lpddr4-evk.conf
require conf/machine/include/tas2562.inc

KMACHINE = "imx8mmevk"
KERNEL_MODULE_AUTOLOAD:imx8mm-jaguar-sentai = " i2c-dev leds-lp50xx spidev snd-aloop libcomposite"

MACHINE_FEATURES:remove:imx8mm-jaguar-sentai = " nxp8987-sdio"

# Need to remove nxpiw612 to build SDK as for some reason it errors with a conflict with linux-firmware
MACHINE_FEATURES:append:imx8mm-jaguar-sentai = " nxpiw612-sdio bgt60 stusb4500 zigbee tas2562 usb-audio-gadget usb-cdc-gadget"

#SKIP_RECIPE[linux-firmware] = "linux-firmware is blacklisted"

# NOTE: This is *not* used for the lmp DISTRO / CI. This is just included for local development with lmp-base
KERNEL_DEVICETREE:append:imx8mm-jaguar-sentai = " imx8mm-jaguar-sentai.dtb"

# Fix imx-vpu-hantro build failure
TOOLCHAIN:pn-imx-vpu-hantro = "gcc"
TOOLCHAIN:pn-imx-vpu-hantro-daemon = "gcc"

# Fix nxp-afe* build failure
TOOLCHAIN:pn-nxp-afe = "gcc"
TOOLCHAIN:pn-nxp-afe-voiceseeker = "gcc"

# Need to have a non-default SPI buffer
KERNEL_MODULE_PROBECONF:append:imx8mm-jaguar-sentai = "spidev"
module_conf_spidev = "options spidev bufsiz=16384"

# USB Gadget support - enables board as USB audio device and CDC serial device
MACHINE_EXTRA_RDEPENDS:append:imx8mm-jaguar-sentai = " usb-audio-gadget usb-cdc-gadget"

# Secure boot SE050 into OpTee (disabled for local development builds)
MACHINE_FEATURES:append:imx8mm-jaguar-sentai = "${@'' if d.getVar('LOCAL_DEVELOPMENT_BUILD') == '1' else ' se05x'}"
# TODO: This line doesn't appear to take effect so overriding it in the OpTee recipe for now
SE05X_OEFID:imx8mm-jaguar-sentai = "0xA200"

SERIAL_CONSOLES = "115200:ttyUSBConsole"