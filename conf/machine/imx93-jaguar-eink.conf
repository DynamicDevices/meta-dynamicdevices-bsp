#@TYPE: Machine
#@NAME: Dynamic Devices i.MX93 Jaguar E-Ink board with LPDDR4X
#@SOC: i.MX93
#@DESCRIPTION: Machine configuration for Dynamic Devices i.MX93 Jaguar E-Ink board with LPDDR4
#@MAINTAINER: Alex J Lennon  <ajlennon@dynamicdevices.co.uk>

MACHINEOVERRIDES =. "imx93-11x11-lpddr4x-evk:"

require conf/machine/imx93-11x11-lpddr4x-evk.conf

KMACHINE = "imx93-11x11-lpddr4x-evk"

MACHINE_FEATURES:remove:imx93-jaguar-eink = " nxp8987-sdio"

# IW612 chipset support for ublox MAYA W2 module (WiFi/BT/802.15.4)
MACHINE_FEATURES:append:imx93-jaguar-eink = " nxpiw612-sdio wifi bluetooth"

#SKIP_RECIPE[linux-firmware] = "linux-firmware is blacklisted"

# NOTE: This is *not* used for the lmp DISTRO / CI. This is just included for local development with lmp-base
KERNEL_DEVICETREE:append:imx93-jaguar-eink = " imx93-jaguar-eink.dtb"

# Fix imx-vpu-hantro build failure
TOOLCHAIN:pn-imx-vpu-hantro = "gcc"
TOOLCHAIN:pn-imx-vpu-hantro-daemon = "gcc"

# Fix nxp-afe* build failure
TOOLCHAIN:pn-nxp-afe = "gcc"
TOOLCHAIN:pn-nxp-afe-voiceseeker = "gcc"

# Need to have a non-default SPI buffer for optimal SPI communication
KERNEL_MODULE_PROBECONF:append:imx93-jaguar-eink = "spidev moal"
module_conf_spidev = "options spidev bufsiz=16384"
module_conf_moal = "options moal drvdbg=0"

# E-ink board specific: Remove audio/camera features, focus on wireless connectivity
MACHINE_FEATURES:remove:imx93-jaguar-eink = " alsa"

SERIAL_CONSOLES = "115200:ttyUSBConsole"

# === Fast Boot Optimizations ===
# Optimize kernel command line for fastest boot
OSTREE_KERNEL_ARGS:imx93-jaguar-eink ?= "console=ttyLP0,115200 rootfstype=ext4 quiet loglevel=3 rw rootwait"

# Load essential modules including wireless connectivity
KERNEL_MODULE_AUTOLOAD:imx93-jaguar-eink = " i2c-dev spidev bluetooth mlan moal"

# Boot time optimizations
OSTREE_BOOTLOADER_EXTRA_CONFIG:imx93-jaguar-eink = "bootdelay=1"
